# H. 判断の書き方


甲州記法は、データを判断形式で表現します。
判断は、なにかが正しいか間違っているかを述べた文に対応する記号表現で、
つぎのような外観をもちます。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|-- 判断種  /項目-1 内容-1  /項目-2 内容-2 /項目-3 内容-3
           /項目-4 内容-4  /項目-5 内容-5
           /項目-6 内容-6
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

これまで例にあわせて説明してきた判断の書き方を、
ここにまとめましょう。

- **判断** は、(1) 判断記号、(2) 判断種、(3) 項目集合
  から構成されます。

- **判断記号** は 2 種類あり、
  正しい判断に対しては **肯定判断記号** `|--` を使い、
  間違った判断に対しては **否定判断記号** `|-X` を使います。

- **データ解釈** となる文のパターンに対する名前として
  **判断種** が与えられ、そのパターンに具体的な
  項目を埋めることで、真偽判断できる文が確定します。

- **項目** は `/項目-1 内容-1` のような
  **項目名** と **項目内容** の組であらわされます。
  項目名を「項目」、項目内容を「内容」と省略することもあります。

- 項目名はスラッシュ `/` から始まります。
  こうすることで、それが項目名であることが明示されます。

- ひとつの判断のなかで、同一の項目名を 2 回使うことは禁止されています。
  どちらの項目を指すのかがわからなくなるからです。

- 項目はどのような順番で書いても構いません。
  個々の項目は項目名で識別され，その順番は関係ありません。

- 判断の項目数が多く、記述が長くなくときは、
  2 行目以降を字下げすることで、複数行に分けて書けます。

- 読みやすさのために、項目名の直前に空白を 2 つ入れることが多いですが、
  1 つでも問題ありません。

- 甲州計算機は、原則として、
  文字コードは UTF-8 を採用しています。
  特定用途向けの甲州計算機は、
  ほかの文字コードの可能性もあります。

- 改行文字として、行送り (LF: line feed) か、
  行復帰 (CR: carriage return) か、
  その組み合わせ (CR + LF) のどれかを使います。

最初は、否定判断記号 `|-X` がなんのためにあるのか、わかりにくいかもしれません。
たとえば、友人と 11 時に待ち合わせを約束していたけれど、
11 時半に変更したいならば、11 時に待ち合わせるという判断を
`|-X` で取り消し、11 時半が正しいと `|--` で宣言するような使い方をします。
ほとんどのデータ処理では，正しい情報の集合を保持しないと意味がないので、
情報は `|--` の肯定判断として表現し、
間違った情報があれば `|-X` で取り消すことになります。

この本に付属するファイルの改行文字は CR + LF です。
もし自分のつくったファイルの文字コードや改行文字が
どのようになっているかがわからないときは、
身近にいるコンピュータに詳しい人にきいてみてください。


## 判断の例

項目 `/a` `/b` `/c` をもつ判断を読み込み、
そのまま書き出すだけのつぎの計算式に対して、
いくつかの判断を与えてみましょう。
この計算は [`koshu H.k`][koshu H.k] により実行できます。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|== ABC : source ABC /a /b /c
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

いままで何回か出てきた、`1` `2` `3` を項目内容とする判断です。
これは、入力がそのまま出力になります。
入力を上段に、出力を下段に書きます。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|-- ABC  /a 1  /b 2  /c 3
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .output }
|-- ABC  /a 1  /b 2  /c 3
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

つぎの判断は、余分は項目 `/d` をもっています。
`source ABC /a /b /c` により読み込んでいるので、
項目 `/d` は捨てられます。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|-- ABC  /a 1  /b 4  /c 5  /d 10
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .output }
|-- ABC  /a 1  /b 4  /c 5
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

逆に、項目が不足していると、
その項目は、内容がないことをあらわす `()` という記号になります。
この記号は空番とよばれます。
空番の項目は出力時に省略されるため、入力と同じ判断が得られます。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|-- ABC  /a 3  /b 2
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .output }
|-- ABC  /a 3  /b 2
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

数値は、読みやすいように、途中に空白を入れられます。
甲州計算機の標準の出力方法でも 3 桁ごとに空白が入ります。
小数として入力された数値は、小数として出力されます。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|-- ABC  /a 10 000  /b -20000  /c + 30000.00
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .output }
|-- ABC  /a 10 000  /b -20 000  /c 30 000.00
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

項目内容を引用符 `'` から始めることで、テキストとして解釈されます。
`'one` は `o` `n` `e` の 3 文字からなるテキストです。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|-- ABC  /a 'one  /b 'two  /c 'three
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .output }
|-- ABC  /a 'one  /b 'two  /c 'three
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

引用符を使って `'two` と書く代わりに、
二重引用符で囲んで `"two"` とも書けます。
途中に空白を含むときは二重引用符で囲む必要があります。
`"one plus two"` は
`o` `n` `e` `⨆` `p` `l` `u` `s` `⨆` `t` `w` `o` の
12 文字からなるテキストです。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|-- ABC  /a 'one  /b "two"
         /c "one plus two"
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .output }
|-- ABC  /a 'one  /b 'two  /c "one plus two"
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

真偽値をあらわすブーリアン (Boolean) は
`(+)` または `(-)` という記号で書きます。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|-- ABC  /a (+)  /b (-)
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .output }
|-- ABC  /a (+)  /b (-)
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


[koshu H.k]: INOUT.md

