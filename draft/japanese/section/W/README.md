# W. 甲州計算機の拡張


## 演算子の追加

これまで、甲州記法の計算式を書くときに、
`meet` や `source` のような関係写像演算子を使ってきました。
この節では、甲州計算機がもっていない関係写像演算子を
追加する方法を取り扱います。
甲州計算機 `koshu` を実装するためのプログラミング言語を使い、
新しい演算子を追加した `koshu-w` というコマンドをつくります。

`koshu` は Haskell というプログラミング言語で実装されています。
この本は甲州記法を説明することが目的なので、実装の詳細にはふれません。
新しい甲州計算機 `koshu-w` は Haskell で書かれた設計書である
[`koshu-w.hs`][koshu-w.hs] から作成される
ということだけ記しておきます。
もし、あなたがプログラミングに詳しく、
Haskell を使える環境をもっているならば、
つぎのようにして `koshu-w` コマンドをつくれます。

``` sh
$ cabal install --bindir=.
```

もし `koshu-w` をつくる手段が手元になくても、
具体的な判断をいくつか書き、
それらを一般化したデータ解釈を与え、
その解釈をみたすように関係写像演算子を定義することで、
甲州計算機を拡張できるということを憶えておいてください。


## 商と余り

`koshu-w` には、割り算を行う `divide`
という関係写像演算子が追加されています。
`divide /x /y /q /r` のように使われ、
入力データに含まれる `/x` と `/y` から
`/x` 割る `/y` を計算し、その商と余りを
項目 `/q` (quotient) と
`/r` (remainder) として追加します。
したがって、`divide /x /y /q /r` は、
つぎのデータ解釈をみたすことになります。

 - `/x` を `/y` で割ると、その商は `/q` で、
   余りは `/r` である。

例として、項目 `/x` と `/y` をもつ 5 つの判断を用意します。

``` text
|-- XY  /x 15  /y 4
|-- XY  /x 15  /y 3
|-- XY  /x 15  /y 2
|-- XY  /x 15  /y 1
|-- XY  /x 15  /y 0
```

これを読み込み、`divide` を使って商 `/q` と余り `/r` を計算します。

``` text
xy   : source XY /x /y
xyqr : divide /x /y /q /r

|== QR -fore /x /y
  | xy | xyqr
```

この判断集合と計算式を保存した [`W.k`][W.k] を
拡張された甲州計算機で実行します。

``` sh
$ ./koshu-w W.k
```

その結果、つぎのような 4 つの判断が出力されます。

``` text
|-- QR  /x 15  /y 4  /q 3  /r 3
|-- QR  /x 15  /y 3  /q 5  /r 0
|-- QR  /x 15  /y 2  /q 7  /r 1
|-- QR  /x 15  /y 1  /q 15  /r 0
```


## ゼロによる割り算

判断集合 `QR` には `/y 0` の判断が含まれていません。
というのは、`0` による割り算ができないからです。
より正確にいうと、つぎのデータ解釈

 - `/x 15` を `/y 0` で割ると、その商は `/q` で余りは `/r` である。

をみたす `/q` と `/r` が整数のなかにはないからです。
整数のなかにはないということは、
`/q` と `/r` について **全称否定判断** が成り立つということです。
全称否定判断とは、すべての何々について、これこれが成り立たないという形式の判断で、
この場合、つぎの判断に相当します。

 - どのような整数 `q` と `r` を選んでも、
   `/x 15` を `/y 0` で割ると、
   その商は `/q q` で余りは `/r r` とはならない。

ここで `/q` と `/r` は 1 つもないという性質を
`()` で置き換えて表現すると、
つぎのような全称否定を含意する肯定判断として書けるでしょう。

 - `/x 15` を `/y 0` で割ると、
   その商は `/q ()` で余りは `/r ()` である。

この判断は、[R 節][R] で計算したのと同じように、
つぎのような形で `maybe xyqr` を使うと出力できます。

``` text
|== QR-MAYBE -fore /x /y
  | xy | maybe xyqr
```

`maybe` や `meet` など、引数に **関係写像** をとる演算子は、
自身へ入力された関係を、引数の関係写像にも与えます。
`xy | maybe xyqr` という式に含まれる各関係写像に対して、
入力される関係と出力される関係を記述してみましょう。

 - 関係写像 `xy : source XY /x /y` は、
   入力関係 R1 を出力関係 R2 に変換します。 
   実際には、関係 R1 は無視され、
   判断集合 `XY` から関係 R2 が作成されます。

 - 関係写像 `xyqr : divide /x /y /q /r` は、
   入力関係 R2 を出力関係 R3 に変換します。
   `maybe xyqr` の入力として受け取った関係 R2 が、
   その引数の関係写像 `xyqr` にも与えられるからです。
   出力関係 R3 には `/y 0` の組が含まれていません。

 - 関係写像 `maybe xyqr` は、
   入力関係 R2 を出力関係 R4 に変換します。
   `xyqr` が R2 から R3 を計算し、
   `maybe` が R2 と R3 を組み合わせて R4 を計算します。

 - 関係 R4 にもとづいて判断集合 `QR-MAYBE` が出力されます。

このなかの関係 R1、R2、R3、R4 は、
具体的には、つぎようになります。

``` text
R1  {| | |}  (reldee)
R2  {| /x : /y | 15 : 4 | 15 : 3 | 15 : 2 | 15 : 1 | 15 : 0 |}
R3  {| /x : /y : /q : /r | 15 : 4 :  3 : 3 | 15 : 3 : 5 : 0
     | 15 :  2 :  7 :  1 | 15 : 1 : 15 : 0 |}
R4  {| /x : /y : /q : /r | 15 : 4 :  3 : 3 | 15 : 3 :  5 : 0
     | 15 :  2 :  7 :  1 | 15 : 1 : 15 : 0 | 15 : 0 : () : () |}
```

関係 R4 に判断種 `QR-MAYBE` が与えられて、
最終的に、つぎの判断集合が出力されます。

``` text
|-- QR-MAYBE  /x 15  /y 4  /q 3  /r 3
|-- QR-MAYBE  /x 15  /y 3  /q 5  /r 0
|-- QR-MAYBE  /x 15  /y 2  /q 7  /r 1
|-- QR-MAYBE  /x 15  /y 1  /q 15  /r 0
|-- QR-MAYBE  /x 15  /y 0  /q ()  /r ()
```


[R]:     https://github.com/seinokatsuhiro/abc-of-koshucode/tree/master/draft/japanese/section/R
[W.k]:   https://github.com/seinokatsuhiro/abc-of-koshucode/blob/master/draft/japanese/section/W/W.k
[koshu-w.hs]:   https://github.com/seinokatsuhiro/abc-of-koshucode/blob/master/draft/japanese/section/W/koshu-w.hs

<!-- ------------------------------------------------------------------
|-- TERM  /ja0 'か  /ja '関係写像          /en "relmap"
|-- TERM  /ja0 'せ  /ja '全称否定判断      /en "universal negative judgement"
------------------------------------------------------------------- -->

