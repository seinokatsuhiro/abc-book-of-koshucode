# E. 条件による選択


この節では、特定の条件をみたすデータを選び出すという計算を行います。
例として、つぎの判断集合から、項目 `/a` と `/b`
がそれぞれ `1` と `2` である判断を選びます。
分かりやすくするために、`**` から始まる注釈をつけています。
注釈は、人間のためのものなので、甲州計算機は無視します。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|-- ABC  /a 1  /b 2  /c 3    ** /a = 1  /b = 2
|-- ABC  /a 1  /b 4  /c 5    ** /a = 1
|-- ABC  /a 3  /b 2  /c 5    **         /b = 2
|-- ABC  /a 4  /b 7  /c 3
|-- ABC  /a 2  /b 7  /c 7
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

演算子 `keep` は条件をみたす組だけを残します。
つぎの例では、`keep` に対して、`|` の左から 5 つの組がわたされ、
その各組に対して、`/a = 1 and /b = 2` を計算し、
この条件をみたす組を選びます。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
abc  : source ABC /a /b /c
abc2 : abc | keep /a = 1 and /b = 2
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

`abc2` の計算結果を `ABC2` という種類の判断として書き出します。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|== ABC2 : abc2
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

このデータと計算式を [`E.k`][E.k] というファイルに保存して、
[`koshu E.k`][koshu E.k] で甲州計算機を実行すると、
ひとつの判断だけが出力されます。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .output }
** -*- koshu -*-
**  
**  INPUT
**    E.k
**    

|-- ABC2  /a 1  /b 2  /c 3

**  
**  SUMMARY
**       1 judge  on ABC2
**       1 judge  in total
**
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


## 単独の条件

`keep` の計算式を少し変えてみましょう。
[`E2.k`][E2.k] は項目 `/a` だけの条件をもちます。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
abc  : source ABC /a /b /c
abc2 : abc | keep /a = 1
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

[`koshu E2.k`][koshu E2.k] は 2 つの判断を出力します。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
|-- ABC2  /a 1  /b 2  /c 3
|-- ABC2  /a 1  /b 4  /c 5
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


## または

[`E3.k`][E3.k] は `and` の代わりに `or` を使っています。
`and` は両方がみたされなければならないのに対して、
`or` はどちらか一方でかまいません。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
abc  : source ABC /a /b /c
abc2 : abc | keep /a = 1 or /b = 2
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

[`koshu E3.k`][koshu E3.k] は、どちらかの条件をみたす判断を出力します。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .output }
|-- ABC2  /a 1  /b 2  /c 3
|-- ABC2  /a 1  /b 4  /c 5
|-- ABC2  /a 3  /b 2  /c 5
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


## 条件式の内容

最後に、`keep` の代わりに `add` を使って、
条件式の計算結果を確認します。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .input }
abc  : source ABC /a /b /c
abcd : abc | add /d ( /a = 1 or /b = 2 )
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

この計算を [`koshu E4.k`][koshu E4.k] で実行すると、項目 `/d` は真か偽をあらわす
`<1>` か `<0>` になります。
真 `<1>` は、特定の `/a` と `/b` に対して、
`/a = 1 or /b = 2` という式が成り立つこと、あるいは、正しいこと、
偽 `<0>` は、この式が成り立たないこと、
あるいは、間違っていることをあらわします。
`keep` は `<1>` の組だけを選び出します。

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ { .koshu .output }
|-- ABCD  /d <1>  /a 1  /b 2  /c 3
|-- ABCD  /d <1>  /a 1  /b 4  /c 5
|-- ABCD  /d <1>  /a 3  /b 2  /c 5
|-- ABCD  /d <0>  /a 4  /b 7  /c 3
|-- ABCD  /d <0>  /a 2  /b 7  /c 7
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


[E.k]:  ../E/E.k
[E2.k]: ../E/E2.k
[E3.k]: ../E/E3.k
[E4.k]: ../E/E4.k
[koshu E.k]: INOUT.md#ek
[koshu E2.k]: INOUT.md#e2k
[koshu E3.k]: INOUT.md#e3k
[koshu E4.k]: INOUT.md#e4k
