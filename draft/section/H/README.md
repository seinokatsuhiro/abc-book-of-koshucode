# H. 判断の書き方


甲州記法は、データを判断形式で表現します。
判断は、なにかが正しいか間違っているかを述べた文に対応する記号表現で、
つぎのような外観をもちます。

``` text
|-- 判断種  /項目-1 内容-1  /項目-2 内容-2 /項目-3 内容-3
           /項目-4 内容-4  /項目-5 内容-5
           /項目-6 内容-6
```

これまで例にあわせて説明してきた判断の書き方を、
ここにまとめましょう。

- **判断** は、(1) 判断記号、(2) 判断種、(3) 項目集合
  から構成されます。

- **データ解釈** となる文のパターンに対する名前として
  **判断種** が与えられ、そのパターンに具体的な
  **項目** を埋めることで、真偽判断できる文が確定します。

- 項目は **項目名** と **項目内容** の組であらわされます。
  項目名を項目、項目内容を内容と、省略して述べることもあります。

- 項目名の先頭にスラッシュ `/` を配置することで、
  それが項目名であることを明示します。

- ひとつの判断のなかで、同一の項目名を 2 回使うことは禁止されています。
  どちらの項目を指すのかがわからなくなるからです。

- 項目はどのような順番で書いても構いません。
  個々の項目は項目名で識別され，その位置は関係ありません。

- 正しい判断に対して、 **肯定判断記号** `|--` を置き、
  間違った判断に対して、 **否定判断記号** `|-X` を置きます。

- 判断の項目数が多く、記述が長くなくときは、
  2 行目以降を字下げすることで、複数行に分けて書けます。

- 読みやすさのために、項目名の直前に空白をふたつ入れることが多いですが、
  ひとつでも問題ありません。

- 甲州記法の処理系である計算機は、原則として、
  文字コードは UTF-8 を採用しています。

- 改行文字として、行送り (LF: line feed) か、
  行復帰 (CR: carriage return) か、
  その組み合わせ (CR + LF) のどれかを使います。

最初は、否定判断記号 `|-X` がなんのためにあるのか、わかりにくいかもしれません。
たとえば、友人と 11 時に待ち合わせを約束していたけれど、
11 時半に変更したいならば、11 時に待ち合わせるという判断を
`|-X` で取り消し、11 時半が正しいと `|--` で宣言するような使い方をします。
ほとんどのデータ処理では，正しい情報の集合を保持しないと意味がないので、
情報は `|--` の肯定判断として表現し、
間違った情報があれば `|-X` で取り消すことになります。

この本に付属するファイルの改行文字は CR + LF です。
もし自分のつくったファイルの文字コードや改行文字が
どのようになっているかがわからないときは、
身近にいるコンピュータに詳しい人にきいてみてください。


## 判断の例

項目 `/a` `/b` `/c` をもつ判断を読み込み、
そのまま書き出すだけのつぎの計算式に対して、
いくつかの判断を与えてみましょう。
この計算は [`koshu H.k`][H.k] により実行できます。

``` text
affirm ABC
  source ABC /a /b /c
```

いままで何回か出てきた、`1` `2` `3` を項目内容とする判断です。
これは、入力がそのまま出力になります。
入力を上段に、出力を下段に書きます。

``` text
|-- ABC  /a 1  /b 2  /c 3
```
``` text
|-- ABC  /a 1  /b 2  /c 3
```

つぎの判断は、余分は項目 `/d` をもっています。
`source ABC /a /b /c` により読み込んでいるので、
項目 `/d` は捨てられます。

``` text
|-- ABC  /a 1  /b 4  /c 5  /d 10
```
``` text
|-- ABC  /a 1  /b 4  /c 5
```

逆に、項目が不足していると、
その項目内容は、値がないことをあらわす `()` という記号になります。
この記号は **ニル** (nil) と読み、なにもないことをあらわします。

``` text
|-- ABC  /a 3  /b 2
```
``` text
|-- ABC  /a 3  /b 2  /c ()
```

項目内容を空にして判断を書いたときも、`()` として読まれます。

``` text
|-- ABC  /a /b () /c
```
``` text
|-- ABC  /a ()  /b ()  /c ()
```

数値は、読みやすいように、途中に空白を入れられます。
甲州計算機の標準の出力方法でも 3 桁ごとに空白が入ります。
小数として入力された数値は、小数として出力されます。

``` text
|-- ABC  /a 10 000  /b -20000  /c + 30000.00
```
``` text
|-- ABC  /a 10 000  /b -20 000  /c 30 000.00
```

項目内容を引用符 `'` から始めることで、テキストとして解釈されます。
`'one` は `o` `n` `e` の 3 文字からなるテキストです。

``` text
|-- ABC  /a 'one  /b 'two  /c 'three
```
``` text
|-- ABC  /a 'one  /b 'two  /c 'three
```

途中に空白を含む `'one plus two` は
`o` `n` `e` `⨆` `p` `l` `u` `s` `⨆` `t` `w` `o` の
12 文字からなるテキストです。

``` text
|-- ABC  /a 'one  /b 'two
         /c 'one plus two
```
``` text
|-- ABC  /a 'one  /b 'two  /c 'one plus two
```

ブーリアン (Boolean、真偽値) は
`#true` または `#false` という記号で書きます。
甲州記法では、ハッシュ `#` から始まる単語は、特別に解釈されます。

``` text
|-- ABC  /a #true  /b #false
```
``` text
|-- ABC  /a #true  /b #false  /c ()
```


[H.k]:  https://github.com/seinokatsuhiro/abc-book-of-koshucode/blob/master/draft/section/H/H.k

